apply plugin: 'maven-publish'
apply plugin: 'signing'


// maven metadata
group = 'net.javapla.database'
//artifactId = 'orm'



java {
  withJavadocJar()
  withSourcesJar()
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      //version = project.version
      
      from components.java
      
      
      versionMapping {
        usage('java-api') {
          fromResolutionOf('runtimeClasspath')
        }
        usage('java-runtime') {
          fromResolutionResult()
        }
      }
      
      pom {
        name = "${rootProject.name}"
        description = 'Extremely simple database ORM'
        url = 'https://github.com/MTDdk/ORM'
        
        scm {
          connection = 'scm:git://github.com/MTDdk/ORM.git'
          developerConnection = 'scm:git:git@github.com:MTDdk/ORM.git'
          url = 'https://github.com/MTDdk/ORM'
        }

        licenses {
          license {
            name = 'GNU LGPL, Version 3.0'
            url = 'http://www.gnu.org/licenses/lgpl-3.0.txt'
          }
        }

        developers {
          developer {
            id = 'MTDdk'
            name = 'MTDdk'
            email = 'alvn@alvn.dk'
          }
        }
        
        issueManagement {
          system = 'Github Issues'
          url = 'https://github.com/MTDdk/ORM/issues'
        }
      }
    }
  }
  
  repositories {
    maven {
      def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
      def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
      url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
      
      gradle.taskGraph.whenReady { taskGraph ->
        if (taskGraph.hasTask(':generatePomFileForMavenJavaPublication') && !taskGraph.hasTask(':publishToMavenLocal')) {
        
          // only do any maven related if we have the needed properties available
          if (project.hasProperty('ossrhUsername') && project.hasProperty('ossrhPassword')) {
            credentials {
              username = ossrhUsername
              password = ossrhPassword
            }
          } else {
            throw new GradleException('You do not have your credentials stated')
          }
        }
      }
      
    }
  }
  
}

signing {
  //required { isReleaseVersion }
  sign publishing.publications.mavenJava
}

javadoc {
  enabled = true
  
  if (JavaVersion.current().isJava9Compatible()) {
    options.addBooleanOption('html5', true)
  }
  
  failOnError = false
}